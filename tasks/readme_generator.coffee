#
# * grunt-readme-generator
# * https://github.com/aponxi/grunt-readme-generator
# *
# * Copyright (c) 2013 Logan Howlett
# * Licensed under the MIT license.
# 
"use strict"
fs = require 'fs'
module.exports = (grunt) ->
  pkg = grunt.config.get ['pkg']
  
  # Please see the Grunt documentation for more information regarding task
  # creation: http://gruntjs.com/creating-tasks
  
  # helper functions

  append_to_file = (output, content) ->
    fs.appendFileSync output, content

  make_anchor = (string) ->
    #  make convert a string like "Special Thanks" to "special-thanks"
    # console.log string
    str = string.replace(/\s+/g, '-').toLowerCase()
    str = "#"+str

  back_to_top = () ->
    str = make_anchor pkg.name
    str += "-"
    result = "[Back To Top](#{str})"

  get_latest_changelog = (prefix, changelog_folder)->
    #  get the latest changelog file and print it to the readme
    # todo: what if the extension is .markdown or something else...
    # currently only .md extensions are supported
    versions_found = []
    grunt.file.recurse changelog_folder, (abspath, rootdir, subdir, filename) ->
      if filename.substring(0,prefix.length) is prefix
        # -3 is for .md part
        version = filename.slice(prefix.length,-3)
        versions_found.push version
    if versions_found.length > 0
      versions_found.sort()
      latest = versions_found[versions_found.length -1]
      latest
    else
      grunt.fail.error "No changelogs are present. Please write a changelog file or fix prefixes."
      false


  generate_banner = (path, banner_file, output) ->
    f = path+"/"+banner_file
    unless grunt.file.exists f
      grunt.fail.error "Source file \"" + f + "\" not found."
    else
      append_to_file output, grunt.file.read f
      append_to_file output, "\n"


  generate_TOC = (files, toc_extra_links, changelog_insert_before, output) ->
    append_to_file output, "## Jump to Section\n\n"
    # console.dir files
    
    for file, title of files
    
      if file is changelog_insert_before
        # release history is generated specially since the latest-changelog is generated dynamically.
        release_title = make_anchor "Release History"
        append_to_file output, "* [#Release History](#{release_title})\n"
      link = make_anchor title
      append_to_file output, "* [#{title}](#{link})\n"

    
    if toc_extra_links.length > 0
      for i in toc_extra_links
        ex = i
        append_to_file output, "* #{ex}\n"
    append_to_file output, "\n"
      
  generate_title = (output, travis, username) ->
    title = pkg.name
    desc = pkg.description
    append_to_file output, "# #{title} "

    if travis
      tra = "[![Build Status](https://secure.travis-ci.org/#{username}/#{title}.png?branch=master)](http://travis-ci.org/#{username}/#{title})"
      append_to_file output, "#{tra}"
    append_to_file output, "\n\n> #{desc}\n\n"

  append = (path, file, title, output) ->
    append_to_file output, "## #{title}\n"
    top = back_to_top()
    append_to_file output, "#{top}\n\n"
    f = path+"/"+file
    unless grunt.file.exists f
      grunt.fail.error "Source file \"" + f + "\" not found."
    else
      append_to_file output, grunt.file.read f 
      append_to_file output, "\n"

  generate_release_history = (prefix, changelog_folder, output) ->

    append_to_file output, "## Release History\n"
    top = back_to_top()
    append_to_file output, "#{top}\n\n"
    append_to_file output, "You can find [all the changelogs here](./#{changelog_folder}).\n"
    latest = get_latest_changelog prefix, changelog_folder
    # todo: only supporting .md format at the moment.
    latest_file = prefix + latest + ".md"
    append_to_file output, "### Latest changelog is for [#{latest}](#{changelog_folder}/latest_file):"
    unless grunt.file.exists latest_file
      grunt.fail.error "Changelog file \"" + latest_file + "\" not found."
    else
      append_to_file output, grunt.file.read latest_file 


  generate_footer = (output) ->
    date = new Date();
    str = "\n--------\nThis readme has been automatically generated by [readme generator](https://github.com/aponxi/grunt-readme-generator) on #{date}."
    append_to_file output, str


  grunt.registerMultiTask "readme_generator", "Generate Readme File", ->
    
    # Merge task-specific and/or target-specific options with these defaults.
    options = @options(
      github_username: "aponxi" # this is mainly for travis link
      output: "README.md" # where readme file should be generated in respect to Gruntfile location
      table_of_contents: on # generate table of contents
      readme_folder: "readme" # where readme partial files are located
      changelog_folder: "changelogs" # where changelog files are located
      changelog_version_prefix: "v" # under changelog folder, there are files like v0.1.0.md if the prefix is "V"
      changelog_insert_before: "legal.md" # I like my legal stuff at the bottom of the readme
      toc_extra_links: [] # Sometimes I like adding quicklinks on the top in table of contents. Table of contents (TOC) must be enabled for this option
      banner: null # I like some ascii art on the top of the readme
      has_travis: on # I use travis a lot and want to have the travis image generated on the top
    )
    # lets clean up the output readme
    grunt.file.write options.output, ""


    files = @data.order
    # generate banner
    if options.banner?
      generate_banner options.readme_folder, options.banner, options.output
    # generate title and description
    generate_title options.output, options.has_travis, options.github_username
    generate_TOC files, options.toc_extra_links, options.changelog_insert_before, options.output

    # Iterate over all specified file groups.
    for file, title of files
      # console.log "file: ", file
      # console.log "title: ", title
      append options.readme_folder, file, title, options.output

    # after writing all the contents add footer
    generate_footer options.output
    # Print a success message.
    grunt.log.writeln "File \"" + options.output + "\" created."

